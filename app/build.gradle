buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://dl.bintray.com/alexeydanilov/maven' }
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.jakewharton.hugo'

Properties signProps = new Properties()
signProps.load(new FileInputStream(file('sign.properties')))

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    defaultConfig {
        applicationId 'com.voqse.nixieclock'
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 4
        versionName '0.2.1-b1'
        vectorDrawables.useSupportLibrary = true
        project.archivesBaseName = "${applicationId}-${versionName}".toString()
    }
    signingConfigs {
        debug {
            storeFile file('debug.jks')
        }
        release {
            storeFile file(signProps['storeFile'])
            storePassword signProps['storePassword']
            keyAlias signProps['keyAlias']
            keyPassword signProps['keyPassword']
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile 'com.evernote:android-job:1.1.4'
    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'com.android.support:design:25.1.0'
    compile 'org.slf4j:slf4j-api:1.7.18'
    compile 'com.danikula.iab:in-app-billing:1.5.1@aar'
    compile('org.bitbucket.yahor_makouski:slf4j-composite:1.2') {
        exclude group: 'com.google.android', module: 'android'
    }
    compile('com.crashlytics.sdk.android:crashlytics:2.6.6@aar') {
        transitive = true;
    }

    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.1.1'
    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile('com.squareup:fest-android:1.0.8') {
        exclude group: 'com.android.support', module: 'support-v4'
    }
}
