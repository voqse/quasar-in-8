buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
    }
}

apply plugin: 'com.android.application'

repositories {
    maven { url 'https://dl.bintray.com/alexeydanilov/maven' }
}

apply plugin: 'com.jakewharton.hugo'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 30
    buildToolsVersion '29.0.3'
    defaultConfig {
        applicationId 'com.voqse.nixieclock'
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 14
        versionName '1.3.12'
        vectorDrawables.useSupportLibrary = true
        project.archivesBaseName = "${applicationId}-${versionName}".toString()
    }
    signingConfigs {
        debug {
            storeFile file('debug.jks')
        }
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation 'com.evernote:android-job:1.1.4'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'org.slf4j:slf4j-api:1.7.18'
    implementation 'com.google.android.gms:play-services-base:17.5.0'
    implementation 'com.danikula.iab:in-app-billing:1.5.2@aar'
    implementation('org.bitbucket.yahor_makouski:slf4j-composite:1.2') {
        exclude group: 'com.google.android', module: 'android'
    }

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:4.2.1'
    testImplementation 'org.mockito:mockito-all:1.9.5'
    testImplementation('com.squareup:fest-android:1.0.8') {
        exclude group: 'com.android.support', module: 'support-v4'
    }
}
